class NoPuff : Actor
{
	Default
	{
		+ALLOWTHRUFLAGS
		+MTHRUSPECIES
		+NOBLOCKMAP
		+NOGRAVITY
	}

	States
	{
		Spawn:
		XDeath:
		Crash:
		Melee:
			TNT1 A 2;
			Stop;
	}
}

class LaserHit : Actor
{
	SparkSpawner sparks;

	Default
	{
		+NOBLOCKMAP
		+NOGRAVITY
		+ALWAYSPUFF
		+INVISIBLE
		+FORCEDECAL
		+PUFFGETSOWNER
		DamageType "Laser";
		Decal "RailScorchLower";
		Radius 0;
		Height 0;
	}

	States
	{
		XDeath:
		Melee:
		Crash:
		Spawn:
			UNKN A 0;
			UNKN A 2;
			Stop;
	}

	override void PostBeginPlay()
	{
		A_PlaySound("laser/hit", CHAN_AUTO, 0.05, false, ATTN_STATIC);

		if (level.time % Random(3, 7) == 0 && self)
		{
			sparks = SparkSpawner(Spawn("SparkSpawner", pos));

			if (sparks)
			{
				sparks.A_SetTics(1);
				sparks.angle = angle;
				sparks.pitch = pitch;
				sparks.silent = true;
			}
		}

		Super.PostBeginPlay();
	}
}

class SmokePuff : Actor
{
	Default
	{
		+FORCEXYBILLBOARD
		+NOINTERACTION
		+WINDTHRUST
		RenderStyle "Translucent";
		Alpha 0.1;
		Scale 0.04;
		VSpeed 1;
		Radius 0;
		Height 0;
	}

	States
	{
		Spawn:
			SMOK # 0;
			SMOK # 1;
		FadeIn:
			SMOK # 0 {
				alpha += 0.04;

				if (alpha < 0.7) { SetStateLabel("FadeIn"); }
			}
		FadeOut:
			SMOK # 3;
			SMOK # 0 {
				alpha -= 0.025;
				Scale *= 1.1;

				if (alpha > 0.1) { SetStateLabel("FadeOut"); }
			}
			Stop;
	}

	override void PostBeginPlay()
	{
		scale.x *= FRandom(0.35, 0.7);
		scale.y *= FRandom(0.35, 0.7);

		frame = Random(0, 2);
	}
}